buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: "org.jetbrains.kotlin", name: "kotlin-gradle-plugin", version: "$kotlin_version"
        classpath group: "org.jetbrains.kotlin", name: "kotlin-allopen", version: "$kotlin_version"
        classpath group: "org.jetbrains.kotlin", name: "kotlin-noarg", version: "$kotlin_version"
    }
}

plugins {
    id "org.springframework.boot" version "$spring_boot_version"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.springdoc.openapi-gradle-plugin" version "1.3.3"
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "jacoco"
}

apply plugin: "kotlin"
apply plugin: "kotlin-noarg"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-spring"

allOpen {
    annotation("javax.persistence.Entity")
    annotation("io.aesy.yumme.dto.Dto")
}

noArg {
    annotation("javax.persistence.Entity")
    annotation("io.aesy.yumme.dto.Dto")
}

group = "io.aesy.yumme"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = JavaVersion.VERSION_17

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

repositories {
    mavenCentral()
}

configurations.all {
    // org.junit:junit can't be excluded due to testcontainers, see github.com/testcontainers/testcontainers-java/issues/970
    exclude group: "org.junit", module: "junit-bom"
    exclude group: "org.mockito", module: "mockito-core"
    exclude group: "org.mockito", module: "mockito-junit-jupiter"
    exclude group: "org.assertj", module: "assertj-core"
}

dependencies {
    api group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "$kotlin_version"
    api group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "$kotlin_version"
    annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor"
    implementation group: "org.springdoc", name: "springdoc-openapi-webmvc-core", version: "1.6.6"
    implementation group: "org.springdoc", name: "springdoc-openapi-kotlin", version: "1.6.5"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-data-jpa"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-data-redis"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-validation"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-web"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-cache"
    implementation group: "org.springframework.boot", name: "spring-boot-actuator"
    implementation group: "org.apache.shiro", name: "shiro-spring-boot-web-starter", version: "1.7.1"
    implementation group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.13" // For PATCH support, see https://bugs.openjdk.java.net/browse/JDK-7016595
    implementation group: "com.fasterxml.jackson.module", name: "jackson-module-kotlin", version: "2.13.1"
    implementation group: "at.favre.lib", name: "bcrypt", version: "0.9.0"
    implementation group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "2.7.3"
    implementation group: "org.flywaydb", name: "flyway-core", version: "8.5.0"
    implementation group: "org.flywaydb", name: "flyway-mysql", version: "8.4.3"
    implementation group: "com.auth0", name: "java-jwt", version: "3.18.3"
    implementation group: "redis.clients", name: "jedis", version: "3.8.0"
    implementation group: "com.amazonaws", name: "aws-java-sdk-s3", version: "1.12.167"

    testImplementation group: "org.springframework.boot", name: "spring-boot-starter-test"
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter", version: "5.8.2"
    testImplementation group: "io.strikt", name: "strikt-core", version: "0.33.0"
    testImplementation group: "io.strikt", name: "strikt-jvm", version: "0.33.0"
    testImplementation group: "io.mockk", name: "mockk", version: "1.12.2"
    testImplementation group: "com.ninja-squad", name: "springmockk", version: "3.1.1"
    testImplementation group: "org.awaitility", name: "awaitility", version: "4.1.1"
    testImplementation group: "org.testcontainers", name: "junit-jupiter", version: "1.16.3"
    testImplementation group: "org.testcontainers", name: "mariadb", version: "1.16.2"
}

jar {
    // https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
    enabled = false
}

openApi {
    apiDocsUrl.set("http://localhost:8080/api/v1/spec")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}
